# Print out all recipes when running `just`
_default:
    @just --list

# Variables
output := "output"
whiskers_cmd := "cargo run --bin whiskers --"

# Create the output directory
setup:
    mkdir -p {{output}}

# Remove all files in the output directory
clean:
    rm -fv {{output}}/*.yml

# Generate a file with a specified flavor and accent
accent f a:
	@{{whiskers_cmd}} input.hbs {{f}}  --overrides '{"accent": "{{{{{{a}}}}" }' > {{output}}/{{f}}-{{a}}.yml

# Generate a single flavor, e.g. "mocha", with all of its accents
flavor f: (accent f "rosewater") (accent f "flamingo") (accent f "pink") (accent f "mauve") (accent f "red") (accent f "maroon") (accent f "peach") (accent f "yellow") (accent f "green") (accent f "teal") (accent f "sky") (accent f "sapphire") (accent f "blue") (accent f "lavender")

# Generate all four flavors
all: setup (flavor "latte") (flavor "frappe") (flavor "macchiato") (flavor "mocha")